buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def publicKey = properties.getProperty('public_key')
    def filePath = properties.getProperty('release.storeFile')
    def storePass = properties.getProperty('release.storePass')
    def alias = properties.getProperty('release.alias')
    def aliasPass = properties.getProperty('release.aliasPass')

    defaultConfig {
        // com.exampleはgoogle play に上げられなかった
        applicationId "jp.daneko.example"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 5
        versionName versions.library
        buildConfigField "String", "PUBLIC_KEY", "\"$publicKey\""
    }

    if (filePath) {
        signingConfigs {
            release {
                storeFile file(filePath)
                storePassword storePass
                keyAlias alias
                keyPassword aliasPass
            }
        }
    }

    buildTypes {
        debug {
            if (filePath) {
                signingConfig signingConfigs.release
            } else {
                signingConfig = android.signingConfigs.debug
            }
        }
        release {
            if (filePath) {
                signingConfig signingConfigs.release
            } else {
                signingConfig = android.signingConfigs.debug
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-assumenosideeffects-rules.pro'
        }
    }

    // 一旦checkコマンドを走らせるためにlint時にabortしないようにする
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenLocal()
}

dependencies {

    compile "com.android.support:appcompat-v7:$versions.supportLib"
    compile project(':library')
    compile "io.reactivex:rxandroid:${versions.rxandroid}"

    // for slf4j
    compile 'com.github.tony19:logback-android-core:1.1.1-3'
    compile 'com.github.tony19:logback-android-classic:1.1.1-3'

    compile 'com.jakewharton:butterknife:7.0.1'

    provided 'org.projectlombok:lombok:1.16.4'
}

retrolambda {
    jdk env.j8Home
    oldJdk env.j7Home
    javaVersion JavaVersion.VERSION_1_7
    //jvmArgs '-arg1', '-arg2'
}

