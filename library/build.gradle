
apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'maven'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode 2
        versionName "0.2.0"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    // 一旦checkコマンドを走らせるためにlint時にabortしないようにする
    lintOptions {
        abortOnError false
    }
}

dependencies {

    compile "io.reactivex:rxjava:${versions.rx}"
    compile "org.functionaljava:functionaljava:${versions.fj}"
    compile 'org.slf4j:slf4j-api:1.7.6'
    compile "com.android.support:support-annotations:${versions.supportLib}"

    provided "org.projectlombok:lombok:${versions.lombok}"
}

retrolambda {
    jdk env.j8Home
    oldJdk env.j7Home
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods true
    incremental false
    //jvmArgs '-arg1', '-arg2'
}

/**
 * archive
 */
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri("${env.m2dir}/repository"))
            pom.version = '0.2.0-SNAPSHOT'
            pom.groupId = 'com.github.daneko'
            pom.artifactId = 'android-inapp-library'
            pom.project {
                inceptionYear '2014'
                packaging 'aar'
                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://github.com/daneko/android-inapp-library/blob/master/LICENSE.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    connection 'scm:https://daneko@github.com/daneko/android-inapp-library'
                    developerConnection 'scm:daneko@github.com/daneko/android-inapp-library.git'
                    url 'https://github.com/daneko/android-inapp-library'
                }
            }
        }
    }
}
