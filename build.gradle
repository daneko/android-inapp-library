// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    versions = [
            buildTools   : '23.0.1',
            compileSdk   : 23,
            minSdk       : 15,
            supportLib  : '23.0.1',
            play_service : '7.8.0',
            lombok       : '1.16.4',
            fj           : '4.4',
    ]

    // local.properties„ÅÆloadl
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    env = [
            sdkDir           : properties.getProperty('sdk.dir') ?: System.getenv("ANDROID_HOME"),
            j8Home           : properties.getProperty('java8_home') ?: System.getenv("JAVA8_HOME"),
            j7Home           : properties.getProperty('java7_home') ?: System.getenv("JAVA7_HOME"),
            m2dir            : properties.getProperty('upload.dir') ?: "/tmp",
    ]
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
        // https://bintray.com/android/android-tools/com.android.tools.build.gradle/view
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all,-serial,-varargs,-processing"
    }
}

task wrapper(type: Wrapper){
  gradleVersion = '2.5'
}
